project.description = 'VIVIDUS plugin for WinRM (Windows Remote Management)'

ext.cxfVersion = '3.6.8'

dependencies {
    implementation project(':vividus-engine')
    implementation project(':vividus-reporter')
    implementation project(':vividus-soft-assert')

    // The latest WinRm4J release (0.12.3) has a bug when certificate checks can't be disabled for Basic Authentication.
    // This bug is fixed in `master` branch: https://github.com/cloudsoft/winrm4j/commit/42c704c19cb3b15bc46030d0dc54cc1df09d3720.
    // Thus it's needed to use Jitpack build with the need fix.
    implementation('com.github.cloudsoft.winrm4j:winrm4j:806a943a3d')

    // Jar Hell brings `org.apache.httpcomponents:httpasyncclient:4.1.5` transitive dependency which has a "fix":
    // https://github.com/apache/httpasyncclient/commit/94b061bae5fe247cf3e69b487a78b457f20ca2e2#diff-7883e3d4bc544069649256a8c5307241332cb06ed434bd613b0aa31630746d05
    // breaking CXF.
    // Apache CXF team implemented the fix: https://github.com/apache/cxf/commit/efa7f6d730fe6e1facf92d86821fa15bc123d201
    // and reported an issue: https://issues.apache.org/jira/browse/HTTPASYNC-168 (Won't Fix).
    // So it's needed to use Apache CXF (`3.4.7` or higher) or (`3.5.2` or higher).
    implementation("org.apache.cxf:cxf-rt-transports-http:${cxfVersion}")
    implementation("org.apache.cxf:cxf-rt-transports-http-hc:${cxfVersion}")
    implementation("org.apache.cxf:cxf-rt-frontend-jaxws:${cxfVersion}")

    implementation('javax.xml.ws:jaxws-api:2.3.1')
    implementation('javax.jws:javax.jws-api:1.1')
    implementation('com.sun.xml.messaging.saaj:saaj-impl:1.5.3')

    testImplementation platform(libs.junit.bom)
    testImplementation(libs.junit.jupiter)
    testRuntimeOnly(libs.junit.platformLauncher)
    testImplementation platform(libs.mockito.bom)
    testImplementation(libs.mockito.junitJupiter)
}
