project.description = 'VIVIDUS to Xray exporter'

buildscript {
    ext {
        springBootVersion = '3.5.4'
    }

    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath(group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: "${springBootVersion}")
    }
}
apply plugin: org.springframework.boot.gradle.plugin.SpringBootPlugin

tasks {
    javadocJar.enabled = false
    sourcesJar.enabled = false
}

dependencies {
    // https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/257
    implementation platform(group: 'org.springframework.boot', name: 'spring-boot-dependencies', version: "${springBootVersion}")

    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter')
    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter-validation')
    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2')

    implementation project(':vividus-util')
    implementation project(':vividus-engine')
    implementation project(':vividus-facade-jira')
    implementation project(':vividus-exporter-commons')

    implementation(group: 'org.apache.commons', name: 'commons-lang3', version: '3.18.0')
    implementation(group: 'org.apache.commons', name: 'commons-collections4', version: '4.5.0')

    implementation(group: 'org.zeroturnaround', name: 'zt-zip', version: '1.17')

    implementation(group: 'net.javacrumbs.json-unit', name: 'json-unit', version: '4.1.1')
    implementation(group: 'com.jayway.jsonpath', name: 'json-path')

    testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-test')
    testImplementation platform(group: 'org.junit', name: 'junit-bom', version: '5.13.4')
    testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
    testImplementation platform(group: 'org.mockito', name: 'mockito-bom', version: '5.19.0')
    testImplementation(group: 'org.mockito', name: 'mockito-junit-jupiter')
    testImplementation(group: 'com.github.valfirst', name: 'slf4j-test', version: '3.0.2')
}

configurations.all {
    exclude module: 'spring-boot-starter-logging'
}

// https://stackoverflow.com/a/62645933/2067574
configurations {
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf {
            it.buildDependencies.getDependencies(null).contains(jar)
        }
        it.outgoing.artifact(bootJar)
    }
}
