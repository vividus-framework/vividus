project.description = 'VIVIDUS to Azure DevOps exporter'

buildscript {
    ext {
        springBootVersion = '3.1.5'
    }

    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath(group: 'org.springframework.boot', name: 'spring-boot-gradle-plugin', version: "${springBootVersion}")
    }
}
apply plugin: org.springframework.boot.gradle.plugin.SpringBootPlugin

dependencies {
    // https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/257
    implementation platform(group: 'org.springframework.boot', name: 'spring-boot-dependencies', version: "${springBootVersion}")

    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter')
    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter-validation')

    implementation platform(group: 'org.slf4j', name: 'slf4j-bom', version: '2.0.9')
    implementation(group: 'org.slf4j', name: 'slf4j-api')
    implementation platform(group: 'org.apache.logging.log4j', name: 'log4j-bom', version: '2.21.0')
    implementation(group: 'org.apache.logging.log4j', name: 'log4j-api')
    implementation(group: 'org.apache.logging.log4j', name: 'log4j-core')
    implementation(group: 'org.apache.logging.log4j', name: 'log4j-slf4j2-impl')

    implementation project(':vividus-http-client')
    implementation project(':vividus-util')
    implementation project(':vividus-engine')
    implementation project(':vividus-exporter-commons')

    implementation platform(group: 'com.fasterxml.jackson', name: 'jackson-bom', version: '2.15.3')
    implementation(group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml')

    testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-test')
    testImplementation platform(group: 'org.junit', name: 'junit-bom', version: '5.10.0')
    testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter')
    testImplementation platform(group: 'org.mockito', name: 'mockito-bom', version: '5.6.0')
    testImplementation(group: 'org.mockito', name: 'mockito-junit-jupiter')
    testImplementation(group: 'com.github.valfirst', name: 'slf4j-test', version: '3.0.1')
}

configurations.all {
    exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
}

configurations.testImplementation {
    exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j18-impl'
    exclude group: 'ch.qos.logback', module: 'logback-classic'
}

// https://stackoverflow.com/a/62645933/2067574
configurations {
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf {
            it.buildDependencies.getDependencies(null).contains(jar)
        }
        it.outgoing.artifact(bootJar)
    }
}
