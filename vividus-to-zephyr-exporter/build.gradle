project.description = 'VIVIDUS to Zephyr exporter'

buildscript {
    ext {
        springBootVersion = '3.5.6'
    }

    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}
apply plugin: org.springframework.boot.gradle.plugin.SpringBootPlugin

tasks {
    javadocJar.enabled = false
    sourcesJar.enabled = false
}

dependencies {
    // https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/257
    implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

    implementation('org.springframework.boot:spring-boot-starter')
    implementation('org.springframework.boot:spring-boot-starter-validation')
    implementation('org.springframework.boot:spring-boot-starter-log4j2')

    implementation project(':vividus-util')
    implementation project(':vividus-facade-jira')
    implementation project(':vividus-exporter-commons')

    implementation('com.google.guava:guava:33.5.0-jre')
    implementation('org.apache.commons:commons-lang3:3.19.0')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation platform('org.junit:junit-bom:6.0.0')
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
    testImplementation platform('org.mockito:mockito-bom:5.20.0')
    testImplementation('org.mockito:mockito-junit-jupiter')
    testImplementation('com.github.valfirst:slf4j-test:3.0.3')
}

configurations.all {
    exclude module: 'spring-boot-starter-logging'
}

// https://stackoverflow.com/a/62645933/2067574
configurations {
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf {
            it.buildDependencies.getDependencies(null).contains(jar)
        }
        it.outgoing.artifact(bootJar)
    }
}
