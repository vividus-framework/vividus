plugins {
    id 'com.bmuschko.docker-remote-api' version '6.7.0'
}

project.description = 'Vividus Docker bundler'

tasks {
    artifactoryPublish.enabled = false
    publish.enabled = false
    testVividusInitialization.enabled = false
}

dependencies {
    implementation project(':vividus')
    implementation project(':vividus-plugin-applitools')
    implementation project(':vividus-plugin-aws-dynamodb')
    implementation project(':vividus-plugin-aws-kinesis')
    implementation project(':vividus-plugin-aws-lambda')
    implementation project(':vividus-plugin-aws-s3')
    implementation project(':vividus-plugin-browserstack')
    implementation project(':vividus-plugin-csv')
    implementation project(':vividus-plugin-datetime')
    implementation project(':vividus-plugin-db')
    implementation project(':vividus-plugin-electron')
    implementation project(':vividus-plugin-email')
    implementation project(':vividus-plugin-excel')
    implementation project(':vividus-plugin-html')
    implementation project(':vividus-plugin-kafka')
    implementation project(':vividus-plugin-lambdatest')
    implementation project(':vividus-plugin-mobile-app')
    implementation project(':vividus-plugin-mongodb')
    implementation project(':vividus-plugin-parquet')
    implementation project(':vividus-plugin-rest-api')
    implementation project(':vividus-plugin-saucelabs')
    implementation project(':vividus-plugin-ssh')
    implementation project(':vividus-plugin-visual')
    implementation project(':vividus-plugin-web-app-to-rest-api')
    implementation project(':vividus-plugin-web-app')
    implementation project(':vividus-plugin-xml')
}

task copyRuntimeLibs(type: Copy) {
    into "$buildDir/docker/libs"
    from configurations.runtimeClasspath
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

task createDockerfile(type: Dockerfile) {
    dependsOn copyRuntimeLibs

    from('adoptopenjdk/openjdk11:alpine-jre')
    label(['maintainer': 'Vividus Team "vividus.team@vividus.dev"'])
    workingDir('vividus')
    copyFile('libs', 'libs/')
    entryPoint('java', '-cp', '/vividus/resources:/vividus/libs/*', 'org.vividus.runner.StoriesRunner')
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    images.add("vividus/vividus:${project.version}")
}

task pushImage(type: DockerPushImage) {
    dependsOn buildImage
    images = buildImage.images
}
