buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:9.4.0'
    }
}

apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin

apply from: "${buildSystemPath}/vividus-library-subproject.gradle"

project.description = 'VIVIDUS Docker bundler'

tasks {
    publish.enabled = false
    javadocJar.enabled = false
    sourcesJar.enabled = false
}

dependencies {
    implementation project(':vividus')
    implementation project(':vividus-plugin-accessibility')

    /*
    Gradle does not support Maven profiles used to fetch dependencies from pom.xml file based on OS, so to avoid
    downlading of eyes-universal-core-binaries (127mb) which is default active profile, we exlude this dependency
    and specify eyes-universal-core-alpine one to be used inside of docker image.
    Please see https://github.com/gradle/gradle/issues/19225
    */
    implementation (project(':vividus-plugin-applitools')) {
        exclude group: 'com.applitools', module: 'eyes-universal-core-binaries'
    }
    implementation(group: 'com.applitools', name: 'eyes-universal-core-alpine', version: versions.eyesUniversal)

    implementation project(':vividus-plugin-avro')
    implementation project(':vividus-plugin-aws-dynamodb')
    implementation project(':vividus-plugin-aws-kinesis')
    implementation project(':vividus-plugin-aws-lambda')
    implementation project(':vividus-plugin-aws-s3')
    implementation project(':vividus-plugin-azure-cosmos-db')
    implementation project(':vividus-plugin-azure-data-factory')
    implementation project(':vividus-plugin-azure-event-grid')
    implementation project(':vividus-plugin-azure-event-hub')
    implementation project(':vividus-plugin-azure-functions')
    implementation project(':vividus-plugin-azure-resource-manager')
    implementation project(':vividus-plugin-azure-storage-account')
    implementation project(':vividus-plugin-azure-storage-queue')
    implementation project(':vividus-plugin-browserstack')
    implementation project(':vividus-plugin-csv')
    implementation project(':vividus-plugin-datetime')
    implementation project(':vividus-plugin-db')
    implementation project(':vividus-plugin-electron')
    implementation project(':vividus-plugin-email')
    implementation project(':vividus-plugin-excel')
    implementation project(':vividus-plugin-html')
    implementation project(':vividus-plugin-json')
    implementation project(':vividus-plugin-kafka')
    implementation project(':vividus-plugin-lambdatest')
    implementation project(':vividus-plugin-lighthouse')
    implementation project(':vividus-plugin-mobile-app')
    implementation project(':vividus-plugin-mobitru')
    implementation project(':vividus-plugin-mongodb')
    implementation project(':vividus-plugin-parquet')
    implementation project(':vividus-plugin-rest-api')
    implementation project(':vividus-plugin-saucelabs')
    implementation project(':vividus-plugin-shell')
    implementation project(':vividus-plugin-ssh')
    implementation project(':vividus-plugin-visual')
    implementation project(':vividus-plugin-web-app-to-rest-api')
    implementation project(':vividus-plugin-web-app')
    implementation project(':vividus-plugin-websocket')
    implementation project(':vividus-plugin-winrm')
    implementation project(':vividus-plugin-xml')
    implementation project(':vividus-plugin-yaml')

    implementation project(':vividus-agent-reportportal')

    implementation(group: 'com.mysql', name: 'mysql-connector-j', version: '9.1.0')
    implementation(group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '12.8.1.jre11')
    implementation(group: 'org.postgresql', name: 'postgresql', version: '42.7.5')
    implementation(group: 'com.oracle.database.jdbc', name: 'ojdbc10', version: '19.25.0.0')
    implementation(group: 'com.ibm.db2.jcc', name: 'db2jcc', version: 'db2jcc4')
    implementation(group: 'net.sourceforge.csvjdbc', name: 'csvjdbc', version: '1.0.46')
    implementation(group: 'com.h2database', name: 'h2', version: '2.3.232')
    implementation(group: 'net.snowflake', name: 'snowflake-jdbc', version: '3.22.0')
}

tasks.register('copyRuntimeLibs', Copy) {
    into layout.buildDirectory.dir('docker/libs')
    from configurations.runtimeClasspath
}

tasks.register('createDockerfile', com.bmuschko.gradle.docker.tasks.image.Dockerfile) {
    dependsOn copyRuntimeLibs

    from('eclipse-temurin:21-jre-alpine')
    label(['maintainer': 'Vividus Team "vividus.team@vividus.dev"'])
    instruction('RUN apk add gcompat')
    workingDir('vividus')
    copyFile('libs', 'libs/')
    entryPoint('java', '-cp', '/vividus/resources:/vividus/libs/*', 'org.vividus.runner.StoriesRunner')
}

tasks.register('buildImage', com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
    dependsOn createDockerfile
    images.add("vividus/vividus:${project.version}")
}

tasks.register('pushImage', com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
    dependsOn buildImage
    images = buildImage.images
}
