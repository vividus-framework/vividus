plugins {
  id 'org.sonarqube' version '5.1.0.4882'
}

ext.buildSystemPath = file("${System.env.VIVIDUS_BUILD_SYSTEM_HOME?:buildSystemRootDir}/${buildSystemVersion}")

apply from: "${buildSystemPath}/gradle/project.gradle"
apply from: "${buildSystemPath}/gradle/jacoco.gradle"

subprojects {
    dependencies {
        modules {
            module('org.hamcrest:hamcrest-core') {
                replacedBy('org.hamcrest:hamcrest', 'Use latest hamcrest version')
            }
            module('commons-logging:commons-logging') {
                replacedBy('org.slf4j:jcl-over-slf4j', 'Redirect commons-logging logs to SLF4J')
            }
            module('org.springframework:spring-jcl') {
                // https://github.com/spring-projects/spring-framework/issues/20611#issuecomment-453462024
                replacedBy('org.slf4j:jcl-over-slf4j', 'Use SLF4J over JCL instead of Spring over JCL implementation')
            }
            module('log4j:log4j') {
                replacedBy('org.slf4j:log4j-over-slf4j', 'Redirect Log4J 1.x logs to SLF4J')
            }
            module('ch.qos.reload4j:reload4j') {
                replacedBy('org.slf4j:log4j-over-slf4j', 'Redirect Reload4j (a drop-in replacement for Log4J 1.x) logs to SLF4J')
            }
            module('org.jsoup:jsoup') {
                replacedBy('com.github.vividus-framework:jsoup', 'Use jsoup build from the custom fork')
            }
            module('com.jayway.jsonpath:json-path') {
                replacedBy('com.github.vividus-framework.JsonPath:json-path', 'Use JsonPath build from the custom fork')
            }
        }
    }
}

def publishedSubprojects = subprojects.findAll {
    it.name != 'vividus-tests' && it.name != 'vividus-docker-bundler'
}
def libraries = publishedSubprojects.findAll { it.name != 'vividus-bom' }
configure(libraries) {
    apply from: "${buildSystemPath}/vividus-library-subproject.gradle"
    java {
        sourceCompatibility = JavaVersion.VERSION_17
    }
}

configure(publishedSubprojects) {
    apply plugin: 'maven-publish'

    publishing {
        repositories {
            maven {
                name = 'GitHubPackages'
                url = 'https://maven.pkg.github.com/vividus-framework/vividus'
                credentials {
                    username = System.getenv('GITHUB_ACTOR')
                    password = System.getenv('GITHUB_TOKEN')
                }
            }
        }
    }
}

tasks.register('jacocoAggregatedReport', JacocoReport) {
    libraries.each {
        executionData it.tasks.withType(Test)
    }
    dependsOn libraries.collect { it.tasks.withType(Test) }
    sourceDirectories.from(libraries.sourceSets.main.java.sourceDirectories)
    classDirectories.from(libraries.sourceSets.main.output)
}

project.description = 'Vividus'

ext {
    versions = [
        jbehave:                '6.0.0-alpha.3',
        powermock:              'a0be5c4bbe',
        // eyes and eyesUniversal versions should be updated and tested together
        eyes:                   '5.69.5',
        eyesUniversal:          '5.64.17',
        jsonPath:               '52ebb195bd'
    ]
}
