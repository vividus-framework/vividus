buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}
ext.buildSystemDir = file("${System.env.BUILD_SYSTEM_ROOT?:buildSystemRootDir}/${buildSystemVersion}")
ext.gradleScriptDir =  new File(ext.buildSystemDir, 'gradle')
apply from: "${gradleScriptDir}/project.gradle"
apply from: "${gradleScriptDir}/jacoco.gradle"
apply from: "${gradleScriptDir}/sonarqube.gradle"

def testProjectPostfix = '-tests'
def nonTestSubprojects = subprojects.findAll { !it.name.endsWith(testProjectPostfix) }

configure(subprojects.findAll { it.name.endsWith(testProjectPostfix) }) {
    apply from: "${buildSystemDir}/vividus-test-subproject.gradle"
}

configure(nonTestSubprojects) {
    apply plugin: 'com.jfrog.bintray'
    apply from: "${buildSystemDir}/vividus-library-subproject.gradle"

    dependencies {
        modules {
            module('org.hamcrest:hamcrest-core') {
                replacedBy('org.hamcrest:hamcrest', 'Use latest hamcrest version')
            }
        }
    }

    bintray {
        user = System.getProperty('bintray.user')
        key = System.getProperty('bintray.key')
        publications = ['maven']

        pkg {
            repo = 'maven-releases'
            name = "${project.group}.${project.name}"
            desc = project.description
            userOrg = 'vividus'
            licenses = ['Apache-2.0']
            websiteUrl = 'https://github.com/vividus-framework/vividus'
            issueTrackerUrl = 'https://github.com/vividus-framework/vividus/issues'
            vcsUrl = 'https://github.com/vividus-framework/vividus.git'
            publicDownloadNumbers = true
            version {
                name = project.version
                vcsTag = "release-${project.version}"
                released  = new Date()
            }
        }
    }
}

task jacocoAggregatedReport(type: JacocoReport) {
    nonTestSubprojects.each {
      executionData it.tasks.withType(Test)
    }
    dependsOn nonTestSubprojects.collect { it.tasks.withType(Test) }
    sourceDirectories.from(subprojects.sourceSets.main.java.sourceDirectories)
    classDirectories.from(subprojects.sourceSets.main.output)
}

project.description = 'Vividus'

sonarqube {
    properties {
        property 'sonar.projectKey', 'vividus-framework_vividus'
    }
}

ext {
    versions = [
        allure:                 '2.13.1',
        commonsIo:              '2.6',
        commonsLang3:           '3.9',
        commonsText:            '1.8',
        groovy:                 '2.5.8',
        guava:                  '28.2-jre',
        hamcrest:               '2.2',
        httpclient:             '4.5.11',
        javaxInject:            '1',
        jbehave:                '4.6.1-alpha.2',
        junit4:                 '4.13',
        slf4j:                  '2.0.0-alpha1',
        spring:                 '5.2.3.RELEASE',
        jsoup:                  '1.12.1',
        junit:                  '5.6.0',
        junit4Dataprovider:     '2.6',
        mockito:                '3.2.4',
        powermock:              '2.0.5',
        slf4jTest:              '2.1.1'
    ]
}
