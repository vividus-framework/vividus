plugins {
  id 'org.sonarqube' version '2.8'
}

ext.buildSystemDir = file("${System.env.BUILD_SYSTEM_ROOT?:buildSystemRootDir}/${buildSystemVersion}")
ext.gradleScriptDir =  new File(ext.buildSystemDir, 'gradle')
apply from: "${gradleScriptDir}/project.gradle"
apply from: "${gradleScriptDir}/jacoco.gradle"

def testProjectPostfix = '-tests'
def nonTestSubprojects = subprojects.findAll { !it.name.endsWith(testProjectPostfix) }

configure(subprojects.findAll { it.name.endsWith(testProjectPostfix) }) {
    apply from: "${buildSystemDir}/vividus-test-subproject.gradle"
}

configure(nonTestSubprojects) {
    apply from: "${buildSystemDir}/vividus-library-subproject.gradle"

    sonarqube {
        properties {
            property 'sonar.moduleKey', project.group + ':' + project.name
        }
    }

    dependencies {
        modules {
            module('org.hamcrest:hamcrest-core') {
                replacedBy('org.hamcrest:hamcrest', 'Use latest hamcrest version')
            }
        }
    }
}

task jacocoAggregatedReport(type: JacocoReport) {
    nonTestSubprojects.each {
      executionData it.tasks.withType(Test)
    }
    dependsOn nonTestSubprojects.collect { it.tasks.withType(Test) }
    sourceDirectories.from(subprojects.sourceSets.main.java.sourceDirectories)
    classDirectories.from(subprojects.sourceSets.main.output)
}

project.description = 'Vividus'

apply plugin: 'org.sonarqube'
sonarqube {
    properties {
        property 'sonar.projectKey', 'vividus-framework_vividus'
    }
}

ext {
    versions = [
        allure:                 '2.13.0',
        aspectj:                '1.9.4',
        commonsIo:              '2.6',
        commonsLang3:           '3.9',
        commonsText:            '1.8',
        groovy:                 '2.5.8',
        guava:                  '28.1-jre',
        hamcrest:               '2.2',
        httpclient:             '4.5.10',
        javaxInject:            '1',
        jbehave:                '4.6.1-alpha',
        junit4:                 '4.12',
        slf4j:                  '2.0.0-alpha1',
        spring:                 '5.2.0.RELEASE',
        jsoup:                  '1.12.1',
        junit:                  '5.5.2',
        junit4Dataprovider:     '2.6',
        mockito:                '3.1.0',
        powermock:              '2.0.2',
        slf4jTest:              '2.1.1'
    ]
}
